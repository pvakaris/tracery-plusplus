grammar uk.ac.kcl.inf.mdd.tracerypp.TraceryPP with org.eclipse.xtext.common.Terminals

generate traceryPP "http://www.ac.uk/kcl/inf/mdd/tracerypp/TraceryPP"

// Version 1.0

Model:
	statements += Statement*
	story = Story
;
	
Statement:
	Declaration
;

Declaration:
	ListDeclaration |
	ObjectDeclaration
;

/*
	Used to define what the story should look like.
	There must be only one story in the model for now.
 */
Story:
	{Story} "The" "story:"
	story += ( Word | StoryVariable | ObjectAttribute )*
;

/*
	Define a variable and give that variable a list of values it can take.
	Examples of use:
		colors can have values: green, blue, orange
		fruit can be: banana or apple or pear
 */
ListDeclaration:
	start_symbol = Variable  ("can" "have" "values:" | "can" "be:") list = WordList
;

ObjectDeclaration:
	name = ID "is" "an" "object."
	"It" "has" "these" "attributes:" attributes = AttributeList
;

/*
	Basic building blocks
 */

Word:
	value = STRING
;

Variable:
	name = ID
;

StoryVariable:
	variable = [Variable]
	modifiers += Modifier*
;

Attribute:
	name = ID |
	name = ID "=" value = STRING
;

ObjectAttribute:
	object = [ObjectDeclaration] "." attribute = [Attribute]
;

WordList:
	words += Word (SeparatorOr words += Word)*
;

AttributeList:
	attributes += Attribute ( SeparatorAnd attributes += Attribute)*
;

SeparatorOr:
	"," | "or"
;

SeparatorAnd:
	',' | "and"
;

Modifier:
	CapitalizationModifier |
	FullCapitalizationModifier |
	PluralityModifier |
	ArticleModifier |
	PastTenseModifier
;

CapitalizationModifier:
	".capitalize"
;

FullCapitalizationModifier:
	".CAPITALIZE"
;

PluralityModifier:
	".s"
;

ArticleModifier:
	".a"
;

PastTenseModifier:
	".ed"
;
