grammar tracerypp.TraceryPlusPlus with org.eclipse.xtext.common.Terminals

generate traceryPlusPlus "http://www.TraceryPlusPlus.tracerypp"

/*
 * Author: Vakaris Paulavicius
 * Version 1.2
 */ 

TraceryPlusPlusProgram:
	// First all the necessary statements are declared and then the story structure is specified
	statements += Statement*
	story = Story?
;
	 
Statement:
	Variable
	// For future releases: Rule (for example if else or something similar). 
;

Variable:
	ListDeclaration |
	ObjectDeclaration |
	SubstoryDeclaration
;

/*
	Pointers to variables
 */
VariableUse:
	ListUse |
	ObjectUse |
	SubstoryUse
;
/*
	Used to define what the story should look like.
	There must be only one story in the model for now.
 */
Story:
	{Story} "The" "story" AssignmentOperator?
	story += ( Word | VariableUse )*
;

/*
	Define a variable and give that variable a list of values it can take.
	Examples of use:
		colors can have values: green, blue, orange
		fruit can be: banana or apple or pear
 */
ListDeclaration:
	name = ID  ("can" "have" "values" | "can" "be" | ("has" | "have") "values") AssignmentOperator? list = WordList
;

/*
	Define an object that has attributes. If the attribute value points to a list, the value will be selected randomly from that list.
	Examples of use:
		hero is an object.
		It has attributes: name = "John", occupation = occupation
 */
ObjectDeclaration:
	"There" ("is" | "are") ("a" | "an")? name = ID
	pronouns = Pronouns ("has" | "have") "attributes"? AssignmentOperator? attributes = AttributeList
;

/*
	A substory that can be used in other substories and the story itself.
	Examples of use:
		substory small:
			"I am a small "figure.color" "figure.shape"."
		end-substory
 */
SubstoryDeclaration:
	"define"? "substory" name = ID ":" story += ( Word | VariableUse )* "end-substory"
;

/*
	Basic building blocks
 */

Word:
	value = STRING
;

Attribute:
	NameExistingListAttribute |
	NameValueAttribute
;
/* NOT IN USE CURRENTLY 

JustNameAttribute:
	name = [ListDeclaration] 
;
*/

NameExistingListAttribute:
	name = ID (AssignmentOperator | "from")? value = [ListDeclaration]
;

NameValueAttribute:
	name = ID AssignmentOperator? value = Word
;

ListUse:
	variable = [ListDeclaration]
	modifiers = ModifierList
;

SubstoryUse:
	"use" ("substory" | "sub")? variable = [SubstoryDeclaration]
;

ObjectUse:
	ObjectAttribute |
	ObjectPronoun
;

ObjectAttribute:
	object = [ObjectDeclaration] "." attribute = [Attribute]
	modifiers = ModifierList
;

ObjectPronoun:
	object = [ObjectDeclaration]
	pronoun = PronounIdentifier
;

WordList:
	words += Word (SeparatorOr words += Word)*
;

AttributeList:
	attributes += Attribute ( SeparatorAnd attributes += Attribute)*
;

/*
	Modifiers
 */
 
Pronouns:
	value = ("He" | "She" | "It" | "They")
;

/*
	Used to get the correct pronoun of the object
 */
enum PronounIdentifier:
	They = ":they" |
	Them = ":them" |
	Their = ":their" |
	Theirs = ":theirs"
;

ModifierList:
	{ModifierList} modifiers += Modifier*
;

/*
	Modifiers to modify attributes or list variables
 */
enum Modifier:
	Capitalize="-capitalize" |
	FullCapitalize="-CAPITALIZE" |
	Plural="-s" |
	Article="-a" |
	PastTense="-ed"
;

/*
	Helpers
 */
 
AssignmentOperator:
	"=" | ":" | "-" | "-->" | "is"? "equal" "to" | "is"
;

SeparatorOr:
	"," | "or" | "||"
;

SeparatorAnd:
	',' | "and" | "&&"
;