grammar tracerypp.TraceryPlusPlus with org.eclipse.xtext.common.Terminals

generate traceryPlusPlus "http://www.TraceryPlusPlus.tracerypp"

// Version 1.0

TraceryPlusPlusProgram:
	statements += Statement*
	story = Story
;
	 
Statement:
	Variable
	// Rule (for example if else or something similar)
;

Variable:
	ListDeclaration |
	ObjectDeclaration
;

/*
	Used to define what the story should look like.
	There must be only one story in the model for now.
 */
Story:
	{Story} "The" "story" AssignmentOperator?
	story += ( Word | ListVariable | ObjectAttribute )*
;

/*
	Define a variable and give that variable a list of values it can take.
	Examples of use:
		colors can have values: green, blue, orange
		fruit can be: banana or apple or pear
 */
ListDeclaration:
	name = ID  ("can" "have" "values" | "can" "be" | ("has" | "have") "values") AssignmentOperator? list = WordList
;

/*
	Define an object that has attributes. If the attribute value is not specified, the attribute name
	must be the same as one of the lists. The value will be selected randomly.
	Examples of use:
		hero is an object.
		It has attributes: name = "John", occupation
 */
ObjectDeclaration:
	"There" ("is" | "are") ("a" | "an")? name = ID
	pronouns = Pronouns ("has" | "have") "attributes"? AssignmentOperator? attributes = AttributeList
;

/*
	Basic building blocks
 */

Word:
	name = STRING
;

ListVariable:
	variable = [ListDeclaration]
	modifiers += Modifier*
;

Attribute:
	JustNameAttribute |
	NameExistingListAttribute |
	NameValueAttribute
;

JustNameAttribute:
	name = ID // Was: name = [ListDeclaration] | Solved in code generation
;

NameExistingListAttribute:
	name = ID (AssignmentOperator | "from")? value = [ListDeclaration]
;

NameValueAttribute:
	name = ID AssignmentOperator? value = Word
;

ObjectAttribute:
	object = [ObjectDeclaration] "." (attribute = [Attribute] | pronoun = PronounIdentifier)
	modifiers += Modifier*
;

WordList:
	words += Word (SeparatorOr words += Word)*
;

AttributeList:
	attributes += Attribute ( SeparatorAnd attributes += Attribute)*
;

/*
	Modifiers
 */
 
Pronouns:
	value = "He" | value = "She" | value = "It" | value = "They"
;

PronounIdentifier:
	name = ("they" | "them" | "their" | "theirs")
;

Modifier:
	CapitalizationModifier |
	FullCapitalizationModifier |
	PluralityModifier |
	ArticleModifier |
	PastTenseModifier
;

CapitalizationModifier:
	".capitalize"
;

FullCapitalizationModifier:
	".CAPITALIZE"
;

PluralityModifier:
	".s"
;

ArticleModifier:
	".a"
;

PastTenseModifier:
	".ed"
;

/*
	Helpers
 */
 
AssignmentOperator:
	"=" | ":" | "-" | "-->" | "equal" "to"
;

SeparatorOr:
	"," | "or" | "||"
;

SeparatorAnd:
	',' | "and" | "&&"
;